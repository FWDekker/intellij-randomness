plugins {
    id "idea"

    id "checkstyle"
    id "findbugs"
    id "pmd"

    id "jacoco"
    id "io.github.ddimtirov.codacy" version "0.1.0"

    id "org.jetbrains.intellij" version "0.2.14"
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    compile group: "com.google.code.findbugs", name: "jsr305", version: findbugsVersion
    compile group: "com.google.code.findbugs", name: "annotations", version: findbugsVersion

    testCompile group: "junit", name: "junit", version: junitVersion
    testCompile group: "org.assertj", name: "assertj-core", version: assertjVersion
    testCompile group: "org.assertj", name: "assertj-swing-junit", version: assertjSwingVersion
}

sourceSets {
    // Change the output directory for the main source set back to the old path
    main.output.classesDir = new File(buildDir, "classes/main")
    test.output.classesDir = new File(buildDir, "classes/test")

    test {
        java.srcDirs = [new File("src/test/unit/java")]
        resources.srcDirs = [new File("src/test/unit/resources")]
    }
    integration {
        java.srcDirs = [new File("src/test/integration/java")]
        resources.srcDirs = [new File("src/test/integration/resources")]

        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    codacy

    integrationCompile.extendsFrom testCompile
    integrationRuntime.extendsFrom testRuntime
}

task integration(type: Test, description: "Runs the integration tests.", group: "Verification") {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}


intellij {
    version = intellijVersion
    updateSinceUntilBuild = false
}

checkstyle {
    ignoreFailures = true
}

findbugs {
    ignoreFailures = true
    includeFilter = file("$rootProject.projectDir/config/findbugs/includeFilter.xml")
}

pmd {
    ignoreFailures = true
    ruleSetFiles = files("$rootProject.projectDir/config/pmd/pmd-rules.xml")
}

jacocoTestReport {
    reports {
        csv.enabled = false
        html.enabled = true
        xml.enabled = true
    }
}
