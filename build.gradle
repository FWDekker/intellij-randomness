/// Plugins
plugins {
    // Compilation
    id "org.jetbrains.kotlin.jvm" version "1.3.61"
    id "org.jetbrains.intellij" version "0.4.15"

    // Tests/coverage
    id "jacoco"

    // Static analysis
    id "io.gitlab.arturbosch.detekt" version "1.2.2"

    // Documentation
    id "org.jetbrains.dokka" version "0.9.18"
}


/// Dependencies
repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect"

    testImplementation group: "com.nhaarman.mockitokotlin2", name: "mockito-kotlin", version: mockitoKotlinVersion
    testImplementation group: "org.assertj", name: "assertj-core", version: assertjVersion
    testImplementation group: "org.assertj", name: "assertj-swing-junit", version: assertjSwingVersion
    testImplementation group: "org.jetbrains.spek", name: "spek-api", version: spekVersion
    testImplementation group: "org.jetbrains.spek", name: "spek-junit-platform-engine", version: spekVersion
    testImplementation group: "org.junit.platform", name: "junit-platform-runner", version: junitRunnerVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-params", version: junitVersion
    testImplementation group: "org.junit.vintage", name: "junit-vintage-engine", version: junitVersion

    detektPlugins group: "io.gitlab.arturbosch.detekt", name: "detekt-formatting", version: detektVersion
}


/// Configuration
// Compilation
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    version = intellijVersion
    updateSinceUntilBuild = false
}

patchPluginXml {
    changeNotes(file("src/main/resources/META-INF/change-notes.html").getText())
    pluginDescription(file("src/main/resources/META-INF/description.html").getText())
}

// Tests/coverage
test {
    useJUnitPlatform {
        includeEngines "junit-vintage", "junit-jupiter", "spek"
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData file("$buildDir/jacoco/test.exec")

    sourceSets sourceSets.main

    reports {
        csv.enabled = false
        html.enabled = false
        xml.enabled = true
        xml.destination = file("$buildDir/reports/jacoco/report.xml")
    }

    dependsOn { test }
}

// Static analysis
detekt {
    toolVersion = detektVersion
    config = files(".config/detekt/.detekt.yml")
    failFast = true
}

// Documentation
dokka {
    outputFormat = "html"
    includes = ["packages.md"]

    jdkVersion = 8

    includeNonPublic = false
    skipDeprecated = false
    reportUndocumented = true
    skipEmptyPackages = true
}
