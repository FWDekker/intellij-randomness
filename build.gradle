/// Plugins
plugins {
    // Compilation
    id "org.jetbrains.kotlin.jvm" version "1.4.31"
    id "org.jetbrains.intellij" version "0.7.2"

    // Tests/coverage
    id "jacoco"

    // Static analysis
    id "io.gitlab.arturbosch.detekt" version "1.16.0"

    // Documentation
    id "org.jetbrains.dokka" version "0.9.18"
}


/// Dependencies
repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: "com.fasterxml.uuid", name: "java-uuid-generator", version: uuidGeneratorVersion
    implementation group: "com.vdurmont", name: "emoji-java", version: emojiVersion

    testImplementation group: "com.nhaarman.mockitokotlin2", name: "mockito-kotlin", version: mockitoKotlinVersion
    testImplementation group: "org.assertj", name: "assertj-core", version: assertjVersion
    testImplementation group: "org.assertj", name: "assertj-swing-junit", version: assertjSwingVersion
    testImplementation group: "org.junit.platform", name: "junit-platform-runner", version: junitRunnerVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
    testImplementation group: "org.junit.vintage", name: "junit-vintage-engine", version: junitVersion
    testImplementation group: "org.spekframework.spek2", name: "spek-dsl-jvm", version: spekVersion
    testRuntimeOnly group: "org.spekframework.spek2", name: "spek-runner-junit5", version: spekVersion

    detektPlugins group: "io.gitlab.arturbosch.detekt", name: "detekt-formatting", version: detektVersion
}


/// Configuration
// Compilation
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    version = intellijVersion
    updateSinceUntilBuild = false
}

patchPluginXml {
    changeNotes(file("src/main/resources/META-INF/change-notes.html").getText())
    pluginDescription(file("src/main/resources/META-INF/description.html").getText())
}

// Tests/coverage
test {
    systemProperty("java.awt.headless", "false")
    systemProperty("spek2.execution.test.timeout", 0)

    useJUnitPlatform {
        includeEngines "junit-vintage", "junit-jupiter", "spek2"
    }

    testLogging {
        events "failed"
        exceptionFormat "full"
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData file("$buildDir/jacoco/test.exec")

    sourceSets sourceSets.main

    reports {
        csv.enabled = false
        html.enabled = true
        xml.enabled = true
        xml.destination = file("$buildDir/reports/jacoco/report.xml")
    }

    dependsOn { test }
}

// Static analysis
detekt {
    toolVersion = detektVersion
    allRules = true
    config = files(".config/detekt/.detekt.yml")
}

// Documentation
dokka {
    outputFormat = "html"
    includes = ["packages.md"]

    jdkVersion = 8 // stdlib documentation is broken in Dokka for JDK 11 (Kotlin/dokka#213)

    includeNonPublic = false
    skipDeprecated = false
    reportUndocumented = true
    skipEmptyPackages = true
}

// Compatibility checks
runPluginVerifier {
    // If latest is 20xx.y, then support at least [20xx-1].[y+1].
    // e.g., if latest is 2020.3, support at least 2019.4 (aka 2020.1)
    ideVersions = ["IC-2020.1.4", "IC-2020.2.4", "IC-2020.3.1", "CL-2020.3"]
}
