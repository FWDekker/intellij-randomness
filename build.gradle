/// Plugins
plugins {
    // Compilation
    id "org.jetbrains.kotlin.jvm" version "1.3.72"
    id "org.jetbrains.intellij" version "0.4.21"

    // Tests/coverage
    id "jacoco"

    // Static analysis
    id "io.gitlab.arturbosch.detekt" version "1.9.0"

    // Documentation
    id "org.jetbrains.dokka" version "0.9.18"
}

apply from: "$rootDir/gradle/scripts/verifier.gradle"


/// Dependencies
repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation group: "com.fasterxml.uuid", name: "java-uuid-generator", version: uuidGeneratorVersion
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect"
    implementation group: "com.vdurmont", name: "emoji-java", version: emojiVersion

    testImplementation group: "com.nhaarman.mockitokotlin2", name: "mockito-kotlin", version: mockitoKotlinVersion
    testImplementation group: "org.assertj", name: "assertj-core", version: assertjVersion
    testImplementation group: "org.assertj", name: "assertj-swing-junit", version: assertjSwingVersion
    testImplementation group: "org.junit.platform", name: "junit-platform-runner", version: junitRunnerVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
    testImplementation group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
    testImplementation group: "org.junit.vintage", name: "junit-vintage-engine", version: junitVersion
    testImplementation group: "org.spekframework.spek2", name: "spek-dsl-jvm", version: spekVersion
    testRuntimeOnly group: "org.spekframework.spek2", name: "spek-runner-junit5", version: spekVersion

    detektPlugins group: "io.gitlab.arturbosch.detekt", name: "detekt-formatting", version: detektVersion
}


/// Configuration
// Compilation
compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

intellij {
    version = intellijVersion
    updateSinceUntilBuild = false
}

patchPluginXml {
    changeNotes(file("src/main/resources/META-INF/change-notes.html").getText())
    pluginDescription(file("src/main/resources/META-INF/description.html").getText())
}

// Tests/coverage
test {
    systemProperty("SPEK_TIMEOUT", 0)
    useJUnitPlatform {
        includeEngines "junit-vintage", "junit-jupiter", "spek2"
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData file("$buildDir/jacoco/test.exec")

    sourceSets sourceSets.main

    reports {
        csv.enabled = false
        html.enabled = true
        xml.enabled = true
        xml.destination = file("$buildDir/reports/jacoco/report.xml")
    }

    dependsOn { test }
}

// Static analysis
detekt {
    toolVersion = detektVersion
    config = files(".config/detekt/.detekt.yml")
    failFast = true
}

// Documentation
dokka {
    outputFormat = "html"
    includes = ["packages.md"]

    jdkVersion = 8 // stdlib documentation is broken in Dokka for JDK 11 (Kotlin/dokka#213)

    includeNonPublic = false
    skipDeprecated = false
    reportUndocumented = true
    skipEmptyPackages = true
}

// Compatibility checks
runPluginVerifier {
    pluginFileName = "$rootProject.name-$version"
    ides = ["IC-2018.1", "IC-2019.1", "IC-2020.1", "CL-2019.3"]
}
