plugins {
    id "idea"

    id "checkstyle"
    id "com.github.spotbugs" version "1.6.11"
    id "pmd"

    id "jacoco"

    id "org.jetbrains.intellij" version "0.4.1"
}

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

dependencies {
    compile group: "com.google.code.findbugs", name: "jsr305", version: findbugsVersion
    compile group: "com.github.spotbugs", name: "spotbugs-annotations", version: spotbugsVersion
    compile group: "net.jcip", name: "jcip-annotations", version: jcipVersion

    testCompile group: "nl.jqno.equalsverifier", name: "equalsverifier", version: equalsVerifierVersion
    testCompile group: "org.assertj", name: "assertj-core", version: assertjVersion
    testCompile group: "org.assertj", name: "assertj-swing-junit", version: assertjSwingVersion
    testCompile group: "org.junit.jupiter", name: "junit-jupiter-api", version: junitVersion
    testCompile group: "org.junit.jupiter", name: "junit-jupiter-engine", version: junitVersion
    testCompile group: "org.junit.jupiter", name: "junit-jupiter-params", version: junitVersion
    testCompile group: "org.junit.vintage", name: "junit-vintage-engine", version: junitVersion
    testCompile group: "org.mockito", name: "mockito-core", version: mockitoVersion
}


test {
    useJUnitPlatform()
}

intellij {
    version = intellijVersion
    updateSinceUntilBuild = false
}

checkstyle {
    ignoreFailures = false
}

spotbugs {
    ignoreFailures = false
    excludeFilter = file("$rootDir/config/spotbugs/excludeFilter.xml")
    includeFilter = file("$rootDir/config/spotbugs/includeFilter.xml")
}

pmd {
    toolVersion = pmdVersion
    ignoreFailures = true
    ruleSetFiles = files(file("$rootDir/config/pmd").listFiles())
        .filter { File f -> f.name.startsWith("pmd-global-") || f.name.startsWith("pmd-main-") }
    ruleSets = []
}

pmdTest {
    ignoreFailures = true
    ruleSetFiles = files(file("$rootDir/config/pmd").listFiles())
        .filter { File f -> f.name.startsWith("pmd-global-") || f.name.startsWith("pmd-test-") }
    ruleSets = []
}

jacocoTestReport {
    executionData file("$buildDir/jacoco/test.exec")

    reports {
        csv.enabled = false
        html.enabled = true
        xml.enabled = true
    }
}
