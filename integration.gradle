/*
 * Configuration file for integration test module.
 */

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom integrationTestCompile, testRuntime
}

sourceSets {
    integrationTest {
        java.srcDirs file("src/integration-test/java")
        resources.srcDirs file("src/integration-test/resources")

        compileClasspath += sourceSets.main.output
        compileClasspath += configurations.compileOnly
        compileClasspath += configurations.testCompileOnly
        runtimeClasspath += compileClasspath
    }
}


task integrationTest(type: Test, description: "Runs the integration tests.", group: "Verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    jacoco {
        append = false
    }
}

pmdIntegrationTest {
    ruleSetFiles = files("$rootDir/config/pmd/test-pmd-rules.xml")
}

jacocoTestReport {
    executionData file("$buildDir/jacoco/integrationTest.exec")
}

integrationTest.mustRunAfter test
check.dependsOn integrationTest
